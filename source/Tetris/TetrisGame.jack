/**
 * a (sort of) 'singleton' class that represents a Tetris game.
*/

class TetrisGame {
  static TetrisGame instance;
  static ScoreBoard score_board;
  static World world;

  field boolean exit;

  constructor TetrisGame new() {
    var int i, j;
    var int i0, j0, i1, j1;

    do Screen.clearScreen();

    do MainWindow.init();
    let score_board = ScoreBoard.new();
    do NextWindow.init();

    do World.newInstance();
    let world = World.getInstance();
    do Block.setWorld(world);

    let exit = false;
    return this;
  }

  method void dispose() {
    do Memory.deAlloc(this);
    return;
  }

  function void newInstance() {
    let instance = TetrisGame.new();
    return;
  }

  function TetrisGame getInstance() {
    return instance;
  }

  method void run() {
    var Block block;
    var char key;
    var int i, j;
    let i = 5;
    let j = 19;

    while (~exit) {
      while((key = 0) & (~exit)) {
        let key = Keyboard.keyPressed();
      }
      if (key = 140) {   // 'ESC'
        let exit = true;
      }
      while(~(Keyboard.keyPressed() = 0)) {
      }

      if (key = 73) {   // 'I'
        let block = Block.new(0);
        do block.draw();
      }
      if (key = 74) {   // 'J'
        let block = Block.new(1);
        do block.draw();
      }
      if (key = 76) {   // 'L'
        let block = Block.new(2);
        do block.draw();
      }
      if (key = 79) {   // 'O'
        let block = Block.new(3);
        do block.draw();
      }
      if (key = 83) {   // 'S'
        let block = Block.new(4);
        do block.draw();
      }
      if (key = 84) {   // 'T'
        let block = Block.new(5);
        do block.draw();
      }
      if (key = 90) {   // 'Z'
        let block = Block.new(6);
        do block.draw();
      }

      if (key = 130) {   // move left
        if(block > 0) {
          do block.moveLeft();
        }
      }
      if (key = 132) {   // move right
        if(block > 0) {
          do block.moveRight();
        }
      }
      if (key = 133) {   // down arrow: soft drop
        if(block > 0) {
          let block = block.softDrop();
        }
      }
      if (key = 32) {   // space: hard drop
        if(block > 0) {
          let block = block.hardDrop();
        }
      }
      if (key = 90) {   // 'Z' : rotate left
        if(block > 0) {
          do block.rotateLeft();
        }
      }
      if (key = 131) {   // up arrow: rotate right
        if(block > 0) {
          do block.rotateRight();
        }
      }

      let key = 0;
    }
    
    if (exit) {
      do MainWindow.gameOver();
    }

    return;
  }
}
